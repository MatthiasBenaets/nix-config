#+title: NixOS Fresh Install Guide
#+description: A basic introductional guide on building a NixOS config on your personal machine
#+author: Matthias Benaets

#+attr_html: :width 800
#+attr_org: :width 800
[[file:rsc/NixOS.svg]]

* Table of Content
:PROPERTIES:
:TOC:      :include all :depth 1 :force (nothing) :ignore (this)
:END:
:CONTENTS:
- [[#nixos][NixOS]]
- [[#home-manager][Home-Manager]]
- [[#flakes][Flakes]]
- [[#resources][Resources]]
:END:

* NixOS
** Introduction
1. Linux distribution based on Nix package manager
2. Supports declarative reproducible system configurations
3. "Unbreakable"
   - Boot to specific configuration generations. (as mentioned above - reproducible)
4. nix-store: no ~/lib~ & ~/usr/lib~. almost non-existant ~/bin~ & ~/usr/bin~. -> ~/nix/store~
5. nix-env: install packages at user level without having to change system state

** Getting Started
*** NixOS Website
- [[https://nixos.org/][NixOS]]
- [[https://nixos.org/manual/nixos/stable][Manual]]: Downloads -> NixOS -> More -> Manual
- [[https://releases.nixos.org/?prefix=nixos][Unstable]]: Downloads -> NixOS -> More -> also available

*** Burning ISO
- balenaEtcher

*** Booting into ISO
**** Via USB
- On boot: Esc / Del / F1-12

**** Virt-Manager
- Download GUI ISO
- Optional minimal ISO
- No password needed
- Root
  - Terminal (Konsole)
  - ~$ sudo su~
- Layout:
  - minimal iso: ~# loadkeys ...~
  - graphical iso: ~# setxkbmap ...~

*** Partitioning
**** GUI: Gparted
- Device -> Create partition table   -> msdos (legacy boot)
                                    -> gpt (uefi boot)
- Add new partition -> fileSystem: ext4/linux-swap
- Manage flags -> boot for ext4 (possible not necessary)
- Label partitions (useful later)

**** Terminal: Parted
*These step are for a fresh installation. Not dual boot*
#+begin_src
  # parted /dev/sda -- mklabel msdos (gpt for uefi)
  # parted /dev/sda -- mkpart primary 1MiB -8GiB (512MiB -8GiB for uefi)
  # parted /dev/sda -- mkpart primary linux-swap -8GiB 100%

  /* extra for UEFI */
  # parted /dev/sda -- mkpart ESP fat32 1Mib 512MiB
  # parted /dev/sda -- set 3 esp on

  # mkfs.ext4 -L nixos /dev/sda1
  # mkswap -L swap /dev/sda2

  /* extra for UEFI */
  # mkfs.fat -F 32 -n boot /dev/sda3
#+end_src

**** Mounting
#+begin_src
  # mount /dev/disk/by-label/nixos /mnt

  /* extra for UEFI */
  # mkdir -p /mnt/boot
  # mount /dev/disk/by-label/boot /mnt/boot

  # swapon /dev/sda2
#+end_src

** Initial Configuration
*** Generate
- Generate default configuration:
  - ~# nixos-generate-config --root /mnt~
- Location:
  - ~# cd /mnt/etc/nixos~

*** Configuration.nix
**** General
- Argument on how to evaluate config:
    - ~{config, pkgs, ...}:~
- Pull in other files used within the config:
    - ~import = [./hardware-configuration.nix];~

**** Boot
***** Legacy
*Only viable if dualbooting linux distributions*
#+begin_src nix
  # Default Grub setup
  boot.loader.grub.enable = true;
  boot.loader.grub.version = 2;
  boot.loader.grub.device = "/dev/vda";
  # Dual booting made easy (Optional)
  boot.loader.grub.useOSProber = true;
  # Dual booting made a bit harder (Extra Optional)
  boot.loader.grub.extraEntries = ''
    menuentry "Windows 10" {
      chainloader (hd0,1)+1
    }
  '';
#+end_src

***** UEFI
*Used for larger boot drives and dual booting with Windows*
#+begin_src nix
  # Default UEFI setup
  boot.loader.systemd-boot.enable = true;
  boot.loader.efi.canTouchEfiVariables = true;
  # Dual Booting using grub
  boot.loader = {
    efi = {
      canTouchEfiVariables = true;
      efiSysMountPoint = "/boot/efi"; # /boot will probably work too
    };
    grub = {                          # Using grub means first 2 lines can be removed
      enable = true;
      #device = ["nodev"];            # Generate boot menu but not actually installed
      devices = ["nodev"];            # Install grub
      efiSupport = true;
      useOSProber = true;             # Or use extraEntries like seen with Legacy
    };                                # OSProber will probably not find windows partition on first install. Just do a rebuild than.
  };
#+end_src

***** Extras
#+begin_src nix
  { pkgs, ... }:

  {
    boot ={
      kernelPackages = pkgs.linuxPackages_latest;       # Get latest kernel
      initrd.kernelModules = ["amdgpu"];                # More on this later on (setting it for xserver)
      loader = {
        #efi = {
          #canTouchEfiVariables = true;
          #efiSysMountPoint = "/boot/efi";
        #};
        grub = {
          #enable = true;
          #devices = ["nodev"];
          #efiSupport = true;
          #useOSProber = true;
          configurationLimit = 5;                       # Limit stored system configurations.
        };                                              # Also exists for systemd-boot
        timeout = 5;                                    # Work for grub and efi boot, time before auto-boot
      };
    };
  }
#+end_src

**** Networking
- Uncomment: ~networking.hostName="nixos";~
- Network card details. Note: some of these options might have moved to ~hardware-configuration.nix~
  - Deprecated but keep: ~networking.useDHCP = false;~
  - Just internet via ethernet: ~networking.interfaces.<networkcard-id>.useDHCP = true;~

***** Extras
#+begin_src nix
  networking = {
    #hostName = "nixos";
    #networkmanager.enable = true;
    interfaces ={
      enp0s3 = {
        #useDHCP = true;
        ipv4.addresses = [ {                  # Of course not compatible with networkmanager
          address = "192.168.0.50";
          prefixLength = 24;
        } ];
      };
    };
    defaultGateway = "192.168.0.1";
    nameservers = [ "1.1.1.1" ];
  };
#+end_src

**** Internationalisation
*Locales, Layouts and Options*
#+begin_src nix
  # Clock
  time.timeZone = "Belgium/Brussels";
  # Locale
  i18n.defaultLocale = "en_US.UTF-8";
  i18n.extraLocaleSettings = {
    LC_TIME = "nl_BE.UTF-8";
    LC_MONETARY = "nl_BE.UTF-8";
  };
  # TTY layout
  console = {
    font = "...";
    keyMap = "...";                           # us / fr / azerty / etc...
  };
  # XServer layout (possibly also sets console now)
  services.xserver.layout = "..."             # us / fr / be / etc..
  # Extra keyboard settings:
  services.xserver.xkbOptions = "eurosign:e"; # For example adds €
#+end_src

**** Display Managers/Desktop Environments/Window Managers
*Default*
#+begin_src nix
  services.xserver.enable = true;
  services.xserver.displayManager.sddm.enable = true;
  services.xserver.desktopManager.plasma5.enable = true;
#+end_src

*Customized*
#+begin_src nix
  services = {
    xserver = {
      enable = true;
      displayManager = {
        lightdm.enable = true;
        defaultSession = “none+bspwm”;
      };
      desktopManager.xfce.enable = true;
      windowManager.bspwm.enable = true;
    };
  };
#+end_src

**** Hardware
***** Audio & Bluetooth
- [[https://nixos.wiki/wiki/PulseAudio][PulseAudio Wiki]]
- [[https://nixos.wiki/wiki/Bluetooth][Bluetooth Wifi (+ configuring it with PulseAudio)]]
- [[https://nixos.wiki/wiki/PipeWire][PipeWire Wiki]]

****** PulseAudio Example
#+begin_src nix
  { pkgs, ... }:

  {
    sound = {
      enable = true;
      mediaKeys.enable = true;
    };
    hardware = {
      pulseaudio = {
        enable = true;
        package = pkgs.pulseaudioFull;
        extraConfig = ''
          load-module module-switch-on-connect
        '';
      };
      bluetooth = {
        enable = true;
        hsphfpd.enable = true;         # HSP & HFP daemon
        settings = {
          General = {
            Enable = "Source,Sink,Media,Socket";
          };
        };
      };
    };
  }
#+end_src

****** Pipewire Example
#+begin_src nix
  services = {
    pipewire = {
      enable = true;
      alsa = {
        enable = true;
        support32Bit = true;
      };
      pulse.enable = true;
      jack.enable = true;
    };
  };
  hardware = {
    bluetooth = {
      enable = true;
      settings = {
        General = {
          Enable = "Source,Sink,Media,Socket";
        };
      };
    };
  };
#+end_src

***** Touchpad
- [[https://search.nixos.org/options?channel=21.11&show=services.xserver.libinput.tapping&from=0&size=50&sort=relevance&type=packages&query=libinput][Libinput Options]]
#+begin_src nix
  services.xserver.libinput = {
    enable = true;
    #tapping = true;
    #naturalScrolling = true;
    #...
  };
#+end_src

**** Users
#+begin_src nix
  users.users.<name> = {
    isNormalUser = true;
    extraGroups = [ "wheel" "video" "audio" "networkmanager" "lp" "scanner"]
    #initialPassword = "password";
    #shell = pkgs.zsh;
  };
#+end_src

**** Packages
#+begin_src nix
  environment.systemPackages = with pkgs; [
    vim
    wget
    git
    #pkgs.firefox
    firefox
  ];
#+end_src

**** StateVersion
- No need to touch this.
- Nothing to do with the version of the system.
- Just tells the version of state/config
- Can be updated to a stable version if you are really sure.
  - Do consult the release notes first.

*** Hardware-configuration.nix
**** Generate
- Also get automatically generated with:
  - ~# nixos-generate-config --root /mnt~
- Should detect mounted drives, device parts, kernelModules, etc.. that are needed
- Can be deleted and regenerated with:
  - ~# nixos-generate-config~

**** File System
- ~$ sudo blkid~
- or just look in gparted
#+begin_src nix
  fileSystems."/" =
    { device = "/dev/disk/by-uuid/e97ad9a8-d84f-4710-b8c9-cfa7707510ca";
      fsType = "ext4";
    };

  #fileSystem."/" =
  #  { device = "/dev/disk/by-label/nixos";
  #    fsType = "ext4";
  #  };
#+end_src

** Installation
*** System
- For initial installation:
  - ~# nixos-install~
- After applying changes to the config:
  - ~# nixos-rebuild switch~
- Lastly: Set a root password

*** Login
**** initialPassword
- Log in with given password at ~users.users.<user>.initialPassword~

**** via TTY
- Ctrl + Alt + F1 -> Log in via root
- ~# passwd <user>~
- Ctrl + Alt + F7 -> Log in via user

** Installing and Declaring Packages
*** Options
- Individually via Nix Package Manager
  - Install: ~$ nix-env -iA nixos.firefox~
  - List: ~$ nix-env -q~
  - Uninstall: ~$ nix-env --uninstall firefox~
- Alternatively you can also use ~$ nix-shell -p <package name>~
- Configuration file: see below

*** Links
- [[https://search.nixos.org/packages][Packages]]
- [[https://search.nixos.org/options?][Options]]
- ~$ man configuration.nix~

*** Declaring Packages
*Installed system-wide with configuration.nix*
#+begin_src nix
  environment = {
    systemPackages = with pkgs; [
      plex
      superTux
    ];
  };

  nixpkgs.config.allowUnfree = true;
#+end_src

*** Declaring Option
*Some packages will also have options to configure it further*
#+begin_src nix
  services = {
    plex = {
      enable = true;
      openFirewall = true;
    };
  };
#+end_src

*** Variables
*Values that can change often or you want to use multiple times*
#+begin_src nix
  let
    rofi-theme = {
      "*" = {
        bg = "#FFFFFF";
      };
    };
  in
  {
    programs.rofi = {
      enable = true;
      theme = rofi-theme;
    };
  }
#+end_src

*** Overlays
*Change packages or add new packages to existing in nix*
- [[https://nixos.wiki/wiki/Overlays][Overlays wiki]]
#+begin_src nix
  nixpkgs.overlays = [
    (self: super: {
      sl = super.sl.overrideAttrs (old: {
        src = super.fetchFromGitHub {
          owner = "mtoyoda";
          repo = "sl";
          rev = "923e7d7ebc5c1f009755bdeb789ac25658ccce03";
          sha256 = "0000000000000000000000000000000000000000000000000000";
        };
      });
    })

    (self: super: {
      discord = super.discord.overrideAttrs (
        _: { src = builtins.fetchTarball {
          url = "https://discord.com/api/download?platform=linux&format=tar.gz";
          sha256 = "0000000000000000000000000000000000000000000000000000"; #52 0's
        }; }
      );
    })
  ];
  #Should be the same for home-manager
#+end_src

*** Applying
- ~$ sudo nixos-rebuild switch~

** Extras
*** Updating & Upgrading
**** Nix-channel
A. ~$ nix-channel --add https://nixos.org/channels/nixos-21.11~
   OR
B. ~$ nix-channel --update~
2. Next rebuild,use the --upgrade flag:
   - ~$ sudo nixos-rebuild --upgrade~

D. Installed through nix-env:
   - ~$ nix-env -u '*'~

**** Configuration.nix
#+begin_src nix
  system.autoUpgrade = {
    enable = true;
    channel = "https://nixos.org/channels/nixos-unstable";
  };
#+end_src

*** Garbage Collection
**** Command-line
- Remove undeclared packages, dependencies and symlinks:
  - ~$ nix-collect-garbage~
- Remove older generations:
  - ~$ nix-collect-garbage --delete-old~
  - List generations:
    - ~$ nix-env --list-generations~
- Remove specific generations or older than ... days:
  - ~$ nix-env --delete-generations 14d~
  - ~$ nix-env --delete-generations 10 11~
  - Optimize store:
    - ~$ nix-store --gc~
- All in one:
  - ~# nix-collect-garbage -d~

**** Configuration.nix
#+begin_src nix
  nix = {
    settings.auto-optimise-store = true;
    gc = {
      automatic = true;
      dates = "weekly";
      options = "--delete-older-than 7d"
    };
  };
#+end_src

* Home-Manager
** Introduction
- It's like configuration.nix, but for the user environment.
- Plenty more options to declare packages
- Also a better way to manage dotfiles

** Getting Started
*** Home-Manager Website
- [[https://github.com/nix-community/home-manager][Github]]
- [[https://nix-community.github.io/home-manager/][Manual]]
- [[https://nix-community.github.io/home-manager/options.html][Appendix A]]
- [[https://nix-community.github.io/home-manager/nixos-options.html][Appendix B]]

*** Setup
**** Initial
*As a user*
- Add the channel: *needs to be run with root privileges if you want to us the NixOS Module*
  - ~$ nix-channel --add https://github.com/nix-community/home-manager/archive/master.tar.gz home-manager~
  - ~$ nix-channel --add https://github.com/nix-community/home-manager/archive/release-21.11.tar.gz home-manager~
- ~$ nix-channel --update~
- Just to be sure, relog.

**** NixOS Module
*Add to configuration.nix*
#+begin_src nix
  let
  in
  {
    imports = [ <home-manager/nixos> ];

    users.users.<name> = {
      isNormalUser = true;
    }

    home-manager.users.<name> = { pkgs, …}: {
      # declared packages. for example:
      home.packages = [ pkgs.atool pkgs.httpie ];
    };
  }
#+end_src

**** Standalone
Installation:
  - ~$ nix-shell '<home-manager>' -A install~
Configuration file:
  - ~$ cd ~/.config/nixpkgs/home.nix~

** Configuration
*** Links
- [[https://nix-community.github.io/home-manager/options.html][Home-Manager Options]]
- ~$ man home-configuration.nix~

*** Declare user packages
#+begin_src nix
  home.packages = with pkgs; [
    firefox
  ];

  services.dunst = {
    enable = true;
  };
#+end_src

*** Applying
- ~$ home-manager switch~

** Dotfiles
*** Copy/Symlink
**** Existing config files
*For example, randomly nicked files used by other (who don't use NixOS)*
#+begin_src nix
  home.file = {
    ".config/alacritty/alacritty.yml".text = ''
      {"font":{"bold":{"style":"Bold"}}}
    '';
  };
#+end_src

**** Stored files
*Which also don't have any links with NixOS*
#+begin_src nix
  home.file.".doom.d" = {
    source = ./doom.d;
    recursive = true;
    onChange = builtins.readFile ./doom.sh;
  };
  home.file.".config/polybar/script/mic.sh"={
    source = ./mic.sh;
    executable = true;
  };
#+end_src

*** Declared
*Example will generate a file .config/bspwm/bspwmrc*
#+begin_src nix
  {
    xsession = {
      windowManager = {
        bspwm = {
          enable = true;
          rules = {
            "Emacs" = {
              desktop = "3";
              follow = true;
              state = "tiled";
            };
            ".blueman-manager-wrapped" ={
              state = "floating";
              sticky = true;
            };
          };
        };
      };
    };
  }
#+end_src

* Flakes
** Introduction
- Flakes are an "upcoming feature" of the Nix package manager.
- Specify code dependencies declaratively (will be stored in flake.lock)
  - For example: home-manager
- Rebuilding and updating whole system made easy
- Very useful tool to build your own config
  - Multiple configs in one
  - People with github dotfiles will feel right at home

** Getting Started
*** Flakes Wiki
- [[https://nixos.wiki/wiki/Flakes][Wiki]]

*** Setup
**** Configuration.nix
#+begin_src nix
  nix = {
    package = pkgs.nixFlakes;
    extraOptions = "experimental-features = nix-command flakes";
  };
#+end_src

**** Generate
*This command will generate a flake.nix and flake.lock file*
- ~cd~ into a location to store in your system
- ~$ nix flake init~

**** Inputs and Outputs
***** Inputs
*attribute set of all the dependencies used in the flake*
#+begin_src nix
  inputs = {
    nixpkgs.url = "github:nixos/nixpkgs/nixos-unstable";
  };
#+end_src

***** Outputs
*function of an argument that uses the inputs for reference*
- Configure what you imported
- Can be pretty much anything: Packages / configurations / modules / etc...

** Configuration
*** NixOS
**** Flake.nix
#+begin_src nix
{
  inputs = {
    nixpkgs.url = "github:nixos/nixpkgs/nixos-unstable";
    #nixpkgs-unstable.url = "github:nixos/nixpkgs/nixpkgs-unstable";
  };
  outputs = { nixpkgs, home-manager, ... }:
    let
      system = "x86_64-linux";
      pkgs = import nixpkgs {
        inherit system;
        config.allowUnfree = true;
      };

      lib = nixpkgs.lib;
    in {
      nixosConfigurations = {
         <user> = lib.nixosSystem {
          inherit system pkgs;
          modules = [ ./configuration.nix ];
        };
        #<second user> = lib.nixosSystem {
        #inherit system;
        #modules = [ ./configuration.nix ];
        #};
      };
    };
}
#+end_src

**** Build
*a ".(#)" will just build host found in location*
*specify host with "<config path>#<host>" appended*
- *optional* ~$ cp /etc/nixos/* <flake location>~
- ~$ nixos-rebuild build --flake .#~
or build and automatically switch
- ~$ sudo nixos-rebuild switch --flake .#~

*** Home-Manager
**** Flake.nix
***** Seperate
#+begin_src nix
  {
    inputs = {
      #other inputs
      home-manager = {
        url = github:nix-community/home-manager;
        inputs.nixpkgs.follows = "nixpkgs";
      };
    };
    outputs = { self, nixpkgs, home-manager, ... }:
      let
        #variables
        system = "x86_64-linux";
        pkgs = nixpkgs.legacyPackages.system.${system};
      in {
        #other outputs
        homeManagerConfigurations = {
          <user> = home-manager.lib.homeManagerConfiguration {
            inherit pkgs;
            extraSpecialArgs = { inherit <variables>; };
            modules = [
              </relative/path/to/home.nix>
              {
                home = {
                  username = “<user>”;
                  homeDirectory = “/home/<user>”;
                  packages = [ pkgs.home-manager ];
                  stateVersion = "22.05";
                };
              }
            ];
          };
        };
      };
  }
#+end_src

***** Inside nixosConfigurations
#+begin_src nix
  {
    inputs = {
      #other inputs
      home-manager = {
        url = github:nix-community/home-manager;
        inputs.nixpkgs.follows = "nixpkgs";
      };
    };
    outputs = { self, nixpkgs, home-manager, ... }:
      let
        #variables
      in {
        nixosConfigurations = {
          <user> = lib.nixosSystem {
            inherit system;
            modules = [
              ./configuration.nix
              home-manager.nixosModules.home-manager {
                home-manager.useGlobalPkgs = true;
                home-manager.useUserPackages = true;
                home-manager.users.<user> = {
                  imports = [ ./home.nix ];
                };
              }
            ];
          };
        };
      };
  }
#+end_src

**** Build
***** Seperate
*This will build a directory with everything home-manager needs. An activation script is also located inside this dir*
- ~$ nix build .#homeManagerConfigurations.<user>.activationPackage~
- ~$ ./result/activate~

Afterwards you will be able to build home-manager from the flake using
- ~$ home-manager switch --flake .#<host>~

***** Inside nixosConfiguraitons
*Can be build with default rebuild command*
- ~$ sudo nixos-rebuild switch --flake .#<host>~

** Updating
*This will update the flake.lock file*
- ~$ nix flake update~
- Now rebuild and switch

** Flake on fresh install
- Boot into ISO
#+begin_src
  $ sudo su
  # nix-env -iA nixos.git
  # git clone <repo url> /mnt/<path>
  # nixos-install --flake .#<host>
  # reboot
  /* login */
  $ sudo rm -r /etc/nixos/configuration.nix
  /* move config to desired location */
#+end_src

* Resources
1. [[https://nixos.org/][NixOS Website]]
2. [[https://nixos.org/learn.html/][NixOS Learn]]
3. [[https://nixos.org/manual/nixos/stable/][NixOS Manual]]
4. [[https://nixos.wiki/wiki/Main_Page][NixOS Wiki]]
5. [[https://nixos.org/guides/nix-pills/][Nix Pills]]
6. [[https://github.com/nix-community/home-manager/][Home-Manager Github]]
7. [[https://nix-community.github.io/home-manager/][Home-Manager Manual]]
8. [[https://nix-community.github.io/home-manager/options.html][Home-Manager Appendix_A]]
9. [[https://nix-community.github.io/home-manager/nixos-options.html][Home-Manager Appendix B]]
10. [[https://nixos.wiki/wiki/Configuration_Collection][List of reference configurations]]
